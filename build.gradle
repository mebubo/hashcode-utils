// bintray plugin is used to upload artifacts to a bintray repo
plugins { id "com.jfrog.bintray" version "1.7.3" }

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'checkstyle'
apply plugin: 'maven-publish'

group = 'org.hildan.hashcode'
version = '5.0.0'
description = 'Utilities for programs solving Google HashCode problems'
ext.labels = ['hashcode', 'google', 'google-hashcode', 'utils', 'input', 'parser', 'solver', 'runner', 'framework']
ext.licenses = ['MIT']

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    checkstyleConfig
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.24'
    compile 'org.jetbrains:annotations:15.0'
    testCompile 'junit:junit:4.+'
    checkstyleConfig 'org.hildan.checkstyle:checkstyle-config:2.2.0'
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.2'
    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, "checkstyle.xml")
}

def getPropOrEnv = { String prop, String envVar, String defaultValue = null ->
    if (project.hasProperty(prop)) {
        return project.property(prop)
    }
    return System.getenv(envVar) ?: defaultValue
}

ext {
    githubUser = getPropOrEnv('githubUser', 'GITHUB_USER')
    githubRepoName = rootProject.name
    githubSlug = "$githubUser/$githubRepoName"
    githubRepoUrl = "https://github.com/$githubSlug"
    ossrhUserToken = getPropOrEnv('ossrhUserToken', 'OSSRH_USER_TOKEN')
    ossrhKey = getPropOrEnv('ossrhKey', 'OSSRH_KEY')
}

def pomConfig = {
    scm {
        url githubRepoUrl
        connection 'scm:git:' + githubRepoUrl + '.git'
        developerConnection 'scm:git:git@github.com:' + githubSlug + '.git'
    }
    licenses {
        license {
            name 'The MIT License'
            url 'https://opensource.org/licenses/MIT'
        }
    }
    developers {
        developer {
            name 'Joffrey Bion'
            email 'joffrey.bion@gmail.com'
        }
    }
}

apply from: 'gradle/scripts/release.gradle'

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier 'sources' }
            artifact javadocJar { classifier 'javadoc' }
            pom.withXml {
                def root = asNode()
                root.appendNode('name', project.name)
                root.appendNode('description', project.description)
                root.appendNode('url', githubRepoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = getPropOrEnv('bintrayUser', 'BINTRAY_USER')
    key = getPropOrEnv('bintrayApiKey', 'BINTRAY_KEY')
    publications = ['maven']
    publish = true

    pkg {
        repo = getPropOrEnv('bintrayRepoLibs', 'BINTRAY_REPO_LIBS')
        name = project.name
        desc = project.description
        labels = project.labels ?: [project.name]
        licenses = project.licenses ?: []

        websiteUrl = githubRepoUrl
        issueTrackerUrl = githubRepoUrl + '/issues'
        vcsUrl = githubRepoUrl + '.git'
        githubRepo = githubSlug

        version {
            desc = project.description
            vcsTag = 'v' + project.version
            gpg { sign = true }
            mavenCentralSync {
                sync = true
                user = ossrhUserToken
                password = ossrhKey
            }
        }
    }
}
bintrayUpload.dependsOn(build)
rootProject.tasks.release.dependsOn(bintrayUpload)
